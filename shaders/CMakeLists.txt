cmake_minimum_required (VERSION 3.8)

project (SHADERS)

set(SHADERS_PROJ_NAME ${PROJECT_NAME})
set(SHADERS_PROJ_NAME ${SHADERS_PROJ_NAME} PARENT_SCOPE)


set(SHADERS_SRC_DIR ${CMAKE_CURRENT_LIST_DIR}/source)
set(SHADERS_INCLUDE_DIRS ${CMAKE_CURRENT_LIST_DIR}/include)
set(SHADERS_OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR}/bin)

if(CMAKE_BUILD_TYPE STREQUAL "MinSizeRel")
    message(FATAL_ERROR "Invalid build type: ${CMAKE_BUILD_TYPE}. Use Debug/RelWithDebInfo (Profile)/Release")
endif()

if (NOT EXISTS ${SHADERS_OUTPUT_DIR})
    message(STATUS "${SHADERS_PROJ_NAME}: Creating ${SHADERS_OUTPUT_DIR}...")

    make_directory(${SHADERS_OUTPUT_DIR})
endif()

option(SHADER_USE_REVERSED_Z "Use Reversed Depth Buffer in Shaders" ON)

set(SHADER_COMPILER $ENV{VULKAN_SDK}/Bin/slangc.exe)
set(SHADER_TARGET spirv)
set(SHADER_ENTRY_NAME main)
set(SHADER_PROFILE spirv_1_4)

set(SHADER_COMPILER_COMMON_FLAGS
    -emit-spirv-directly
    -fvk-use-entrypoint-name
    -I${SHADERS_INCLUDE_DIRS}
    -target ${SHADER_TARGET}
    -entry ${SHADER_ENTRY_NAME}
    -profile ${SHADER_PROFILE}
)

set(SHADER_COMPILER_FLAGS
    ${SHADER_COMPILER_COMMON_FLAGS}

    $<$<CONFIG:Debug>:-O0>
    $<$<CONFIG:Debug>:-g>
    $<$<CONFIG:Debug>:-DENV_DEBUG>

    $<$<OR:$<CONFIG:Release>,$<CONFIG:RelWithDebInfo>>:-O>
)

if (SHADER_USE_REVERSED_Z)
    set(SHADER_COMPILER_FLAGS
        ${SHADER_COMPILER_FLAGS}
        -DENV_REVERSED_Z
    )
endif()

message(STATUS "${SHADERS_PROJ_NAME}: COMPILER: ${SHADER_COMPILER}")
message(STATUS "${SHADERS_PROJ_NAME}: FLAGS: ${SHADER_COMPILER_FLAGS}")
message(STATUS "${SHADERS_PROJ_NAME}: SOURCE DIRS: ${SHADERS_SRC_DIR}")
message(STATUS "${SHADERS_PROJ_NAME}: INCLUDE DIRS: ${SHADERS_INCLUDE_DIRS}")
message(STATUS "${SHADERS_PROJ_NAME}: OUTPUT DIRS: ${SHADERS_OUTPUT_DIR}")

file(GLOB_RECURSE SHADER_SRC_FILES
    ${SHADERS_SRC_DIR}/*.slang
)

foreach(SHADER_SRC_FILE ${SHADER_SRC_FILES})
    get_filename_component(FILE_NAME ${SHADER_SRC_FILE} NAME_WLE)
    
    set(SHADER_SPIRV_FILE ${SHADERS_OUTPUT_DIR}/${FILE_NAME}.spv)
    
    add_custom_command(
        OUTPUT ${SHADER_SPIRV_FILE}
        COMMAND ${SHADER_COMPILER} ${SHADER_SRC_FILE} ${SHADER_COMPILER_FLAGS} -o ${SHADER_SPIRV_FILE}
        DEPENDS ${SHADER_SRC_FILE}
        COMMENT "Compiling ${SHADER_SRC_FILE} -> ${SHADER_SPIRV_FILE}"
    )
    list(APPEND SPIRV_BINARY_FILES ${SHADER_SPIRV_FILE})
endforeach(SHADER_SRC_FILE)

add_custom_target(${SHADERS_PROJ_NAME} DEPENDS ${SPIRV_BINARY_FILES})
