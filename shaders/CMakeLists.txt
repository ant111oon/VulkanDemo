cmake_minimum_required (VERSION 3.8)

project (SHADERS)


option(SHADER_USE_REVERSED_Z "Use Reversed Depth Buffer in Shaders" ON)


set(SHADERS_PROJ_NAME ${PROJECT_NAME})
set(SHADERS_PROJ_NAME ${SHADERS_PROJ_NAME} PARENT_SCOPE)

set(SHADERS_PROJ_DIR ${CMAKE_CURRENT_LIST_DIR})
set(SHADERS_SRC_DIR ${SHADERS_PROJ_DIR}/source)
set(SHADERS_INCLUDE_DIR ${SHADERS_PROJ_DIR}/include)

set(SHADERS_OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR})
set(SHADERS_OUTPUT_BIN_DIR ${SHADERS_OUTPUT_DIR}/bin)


set(SHADER_COMPILER $ENV{VULKAN_SDK}/Bin/slangc.exe)
set(SHADER_TARGET spirv)
set(SHADER_ENTRY_NAME main)
set(SHADER_PROFILE spirv_1_5)

set(SHADER_COMPILER_COMMON_FLAGS
    -emit-spirv-directly
    -I${SHADERS_INCLUDE_DIR}
    -target ${SHADER_TARGET}
    -profile ${SHADER_PROFILE}
)

set(SHADER_COMPILER_FLAGS
    ${SHADER_COMPILER_COMMON_FLAGS}

    $<$<CONFIG:Debug>:-O0>
    $<$<CONFIG:Debug>:-g>
    
    $<$<CONFIG:Debug>:-DENV_DEBUG>
    $<$<CONFIG:RelWithDebInfo>:-DENV_PROFILE>
    $<$<CONFIG:Release>:-DENV_RELEASE>

    $<$<OR:$<CONFIG:Release>,$<CONFIG:RelWithDebInfo>>:-O>
)

if (SHADER_USE_REVERSED_Z)
    set(SHADER_COMPILER_FLAGS
        ${SHADER_COMPILER_FLAGS}
        -DENV_REVERSED_Z
    )
endif()


if(CMAKE_BUILD_TYPE STREQUAL "MinSizeRel")
    message(FATAL_ERROR "Invalid build type: ${CMAKE_BUILD_TYPE}. Use Debug/RelWithDebInfo (Profile)/Release")
endif()

if (NOT EXISTS ${SHADERS_OUTPUT_BIN_DIR})
    message(STATUS "${SHADERS_PROJ_NAME}: Creating ${SHADERS_OUTPUT_BIN_DIR}...")
    make_directory(${SHADERS_OUTPUT_BIN_DIR})
endif()


message(STATUS "${SHADERS_PROJ_NAME}: COMPILER: ${SHADER_COMPILER}")
message(STATUS "${SHADERS_PROJ_NAME}: FLAGS: ${SHADER_COMPILER_FLAGS}")
message(STATUS "${SHADERS_PROJ_NAME}: SOURCE DIR: ${SHADERS_SRC_DIR}")
message(STATUS "${SHADERS_PROJ_NAME}: INCLUDE DIR: ${SHADERS_INCLUDE_DIR}")
message(STATUS "${SHADERS_PROJ_NAME}: OUTPUT DIR: ${SHADERS_OUTPUT_BIN_DIR}")


file(GLOB_RECURSE SHADER_IMPORT_FILES_LIST
    ${SHADERS_INCLUDE_DIR}/*.slang
)


file(GLOB_RECURSE SHADER_SRC_FILES_LIST
    ${SHADERS_SRC_DIR}/*.vs.slang
    ${SHADERS_SRC_DIR}/*.ps.slang
    ${SHADERS_SRC_DIR}/*.cs.slang
)


foreach(SHADER_SRC_FILE ${SHADER_SRC_FILES_LIST})
    get_filename_component(SHADER_SRC_FILE_NAME ${SHADER_SRC_FILE} NAME_WLE)

    set(SHADER_OUTPUT_FILE_NO_EXT ${SHADERS_OUTPUT_BIN_DIR}/${SHADER_SRC_FILE_NAME})
    set(SHADER_SPIRV_FILE ${SHADER_OUTPUT_FILE_NO_EXT}.spv)
    set(SHADER_REFLECTION_FILE ${SHADER_OUTPUT_FILE_NO_EXT}.json)

    set(SHADER_COMPILER_CMD
        ${SHADER_COMPILER} 
        ${SHADER_SRC_FILE}
        ${SHADER_COMPILER_FLAGS}
        -fvk-use-entrypoint-name
        -entry ${SHADER_ENTRY_NAME}
        -o ${SHADER_SPIRV_FILE}
        -reflection-json ${SHADER_REFLECTION_FILE}
    )
    
    add_custom_command(
        OUTPUT ${SHADER_SPIRV_FILE} ${SHADER_REFLECTION_FILE}
        COMMAND ${SHADER_COMPILER_CMD}
        DEPENDS ${SHADER_SRC_FILE} ${SHADER_IMPORT_FILES_LIST}
        COMMENT "${SHADERS_PROJ_NAME}: Compiling ${SHADER_SRC_FILE} -> ${SHADER_SPIRV_FILE}"
    )

    list(APPEND SHADER_SPIRV_BINARY_FILES_LIST ${SHADER_SPIRV_FILE})
endforeach()

add_custom_target(${SHADERS_PROJ_NAME} 
    DEPENDS ${SHADER_SPIRV_BINARY_FILES_LIST} ${SHADER_IMPORT_FILES_LIST}
)
